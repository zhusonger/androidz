apply plugin: 'com.android.application'
apply plugin: 'cn.com.lasong.time'
apply plugin: 'cn.com.lasong.inject'

android {
    compileSdkVersion rootProject.ext.compileSdkVersion
    buildToolsVersion rootProject.ext.buildToolsVersion


    defaultConfig {
        applicationId "cn.com.lasong"
        minSdkVersion rootProject.ext.minSdkVersion
        targetSdkVersion rootProject.ext.targetSdkVersion
        versionCode 1
        versionName "1.0"

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
}


repositories {
    flatDir {
        dirs 'libs'
    }
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar', '*.aar'])

    testImplementation 'junit:junit:4.12'
    androidTestImplementation 'androidx.test.ext:junit:1.1.1'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.2.0'

    implementation 'androidx.appcompat:appcompat:1.1.0'
    implementation "androidx.recyclerview:recyclerview:1.1.0"
    implementation 'cn.com.lasong:base:0.0.2'
    implementation 'cn.com.lasong:widget:0.0.4'
    implementation 'cn.com.lasong:media:0.0.4'
//    implementation project(":base")
//    implementation project(":widget")
//    implementation project(":media")

    // 单独添加核心 module Transferee, 之后至少还需要添加以下三种图片加载器中的一种
    implementation "com.github.Hitomis.transferee:Transferee:1.6.1@aar"
    // 添加 Glide 图片加载器
    implementation 'com.github.Hitomis.transferee:GlideImageLoader:1.6.1'

    implementation 'com.github.bumptech.glide:glide:4.11.0'
    annotationProcessor 'com.github.bumptech.glide:compiler:4.11.0'

    implementation 'com.github.MZCretin:CitySelect:1.0.5'
}

allInjects {
    // 是否开启debug 目前主要打印日志
    injectDebug true
    // 注入的节点, 区分注入的对象
    injectDomains {
        Transferee {
            group "com.github.Hitomis.transferee:Transferee:1.6.1"
            // 需要修改的字节码包
            clzModify = [
                    [
                            className     : 'com.hitomi.tilibrary.transfer.TransferAdapter',
                            modifiers: "public",
                            // 修改方法
                            injectList : [
                                    [
                                            name   : "newParentLayout",
                                            params : "(Landroid.view.ViewGroup;I)",
                                            newName: "newParentLayout_impl"
                                    ],
                                    [
                                            action : "ADD_METHOD",
                                            content:
                                                    """
                                                    private FrameLayout newParentLayout(android.view.ViewGroup container, int pos){
                                                        FrameLayout parentLayout = newParentLayout_impl(container, pos);
                                                        
                                                        View contentView = parentLayout.getChildAt(0);
                                                        contentView.setBackgroundColor(Color.parseColor("#80000000"));
                                                        
                                                        Context context = container.getContext();
                                                        TransferConfig config = transfer.getTransConfig();
                                                        
                                                        ImageView ivBg = new ImageView(context);
                                                        ivBg.setLayoutParams(new android.widget.FrameLayout.LayoutParams(android.view.ViewGroup.LayoutParams.MATCH_PARENT,
                                                         android.view.ViewGroup.LayoutParams.MATCH_PARENT));
                                                        parentLayout.addView(ivBg, 0);
                                                       
                                                        String url = (String) config.getSourceUrlList().get(pos);
                                                        // com.youxi.yxapp.utils.glide.GlideUtils.loadViewAppleBlue(context, url, ivBg);
                                                        ivBg.setBackgroundColor(Color.parseColor("#FFFFFF"));           
                                                        return parentLayout;
                                                    }
                                                    """
                                    ],
                                    [
                                            name   : "getImageItem",
                                            params : "(I)",
                                            type   : "setBody",
                                            content:
                                                    """{
                                                    FrameLayout parentLayout = (FrameLayout) containLayoutArray.get(\$1);
                                                    if (parentLayout != null && parentLayout.getChildAt(1) instanceof TransferImage) {
                                                        return ((TransferImage) parentLayout.getChildAt(1));
                                                    }
                                                    return null;
                                                    }"""
                                    ]
                            ]
                    ]
            ]
        }

        CitySelect {
            group "com.github.MZCretin:CitySelect:1.0.5"
            // 需要修改的字节码包
            clzModify = [
                    [
                            className: "com.cretin.tools.cityselect.view.CitySelectView",
                            injectList : [
                                    [
                                            name: "init",
                                            params: "(Landroid/content/Context;Landroid/util/AttributeSet;I)",
                                            type: "insertBefore",
                                            content: "android.view.View.inflate(\$1, 123, this);"
                                    ],
                                    [
                                            name: "init",
                                            params: "(Landroid/content/Context;Landroid/util/AttributeSet;I)",
                                            type: "deleteAt",
                                            lineRange: "2#1"
                                    ]
                            ]
                    ],
                    [
                            className: "com.cretin.tools.cityselect.view.FastIndexView",
                            injectList : [
                                    [
                                            isConstructor : true,
                                            params: "(Landroid/content/Context;Landroid/util/AttributeSet;I)",
                                            type: "deleteAt",
                                            lineRange: "6"
                                    ],
                                    [
                                            isConstructor : true,
                                            params: "(Landroid/content/Context;Landroid/util/AttributeSet;I)",
                                            type: "insertAt",
                                            lineNum:6,
                                            content: """selectedColor = Color.parseColor("#999999");"""
                                    ]
                            ]
                    ]
            ]
        }
    }
}